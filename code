#include <MCUFRIEND_kbv.h>
#include <Adafruit_GFX.h>
#include <EEPROM.h>

MCUFRIEND_kbv tft;

// === Color Definitions ===
#define COLOR_BLACK  0x0000
#define COLOR_WHITE  0xFFFF

// === Analog Input Pins
#define MAP_PIN   A6
#define IAT_PIN   A7
#define OIL_PIN   A5
#define FUEL_PIN  A4

// === Other I/O
#define VSS_PIN    10
#define BTN_PIN    11
#define LED_PIN    12

// === Calibration Constants
#define OIL_ZERO_ADC 102
#define OIL_FULL_ADC 921
#define OIL_MAX_PSI  100.0

#define MAP_ZERO_ADC 102
#define MAP_FULL_ADC 921
#define MAP_MIN_KPA 50.0
#define MAP_MAX_KPA 400.0
float ambient_kPa = 101.0;

int fuelEmptyADC = 614;
int fuelFullADC  = 82;

const float R_PULL = 10000.0;
const double A_coef = 0.00129, B_coef = 0.000266, C_coef = 0.000000122;

// === Speed / Trip Tracking
volatile unsigned long pulseCount = 0;
unsigned long tripPulseCount = 0;
unsigned long lastSavedPulseCount = 0;
unsigned long lastSpeedMillis = 0;
const unsigned long speedInterval = 500;
const int EEPROM_ADDR = 0;

// === Debounce
bool lastButtonState = HIGH;
unsigned long lastDebounceTime = 0;
const unsigned long debounceDelay = 50;
unsigned long startTime;

void onVSSPulse() {
  pulseCount++;
  tripPulseCount++;
}

void setup() {
  Serial.begin(9600);

  pinMode(VSS_PIN, INPUT_PULLUP);
  pinMode(BTN_PIN, INPUT_PULLUP);
  pinMode(LED_PIN, OUTPUT);
  attachInterrupt(digitalPinToInterrupt(VSS_PIN), onVSSPulse, FALLING);

  EEPROM.get(EEPROM_ADDR, tripPulseCount);
  lastSavedPulseCount = tripPulseCount;

  digitalWrite(LED_PIN, HIGH);
  startTime = millis();

  uint16_t ID = 0x9341;
  Serial.print("Forcing TFT ID = 0x");
  Serial.println(ID, HEX);

  tft.begin(ID);
  tft.setRotation(0);
  tft.fillScreen(COLOR_BLACK);
  tft.setTextSize(3);
  tft.setTextColor(COLOR_WHITE, COLOR_BLACK);

  // Read ambient MAP pressure (engine off)
  int raw = analogRead(MAP_PIN);
  float pressure_kPa = (raw - MAP_ZERO_ADC) * (MAP_MAX_KPA - MAP_MIN_KPA) / (MAP_FULL_ADC - MAP_ZERO_ADC) + MAP_MIN_KPA;
  if (pressure_kPa > 80 && pressure_kPa < 120) ambient_kPa = pressure_kPa;
}

void loop() {
  unsigned long now = millis();
  if (now - startTime >= 4000) digitalWrite(LED_PIN, LOW);

  if (now - lastSpeedMillis >= speedInterval) {
    noInterrupts();
    unsigned long pulses = pulseCount;
    pulseCount = 0;
    interrupts();

    float mph = pulses * (1000.0 / speedInterval) * 0.45;
    float tripMiles = tripPulseCount / 8000.0;

    int rawOil = analogRead(OIL_PIN);
    float oilPsi = (rawOil - OIL_ZERO_ADC) * (OIL_MAX_PSI / (OIL_FULL_ADC - OIL_ZERO_ADC));
    if (oilPsi < 0) oilPsi = 0;

    int rawFuel = analogRead(FUEL_PIN);
    int fuelPercent = map(rawFuel, fuelFullADC, fuelEmptyADC, 100, 0);
    fuelPercent = constrain(fuelPercent, 0, 100);

    int rawMAP = analogRead(MAP_PIN);
    float pressure_kPa = (rawMAP - MAP_ZERO_ADC) * (MAP_MAX_KPA - MAP_MIN_KPA) / (MAP_FULL_ADC - MAP_ZERO_ADC) + MAP_MIN_KPA;
    float boost_kPa = pressure_kPa - ambient_kPa;
    if (boost_kPa < 0) boost_kPa = 0;
    float boostPsi = boost_kPa / 6.89476;

    int rawIAT = analogRead(IAT_PIN);
    float Vout = rawIAT * (5.0 / 1023.0);
    float Rth = R_PULL * (Vout / (5.0 - Vout));
    double lnR = log(Rth);
    double invT = A_coef + B_coef * lnR + C_coef * pow(lnR, 3);
    double tempK = 1.0 / invT;
    double tempF = (tempK - 273.15) * 9.0 / 5.0 + 32.0;

    // === Display Output ===
    tft.setCursor(10, 10);
    tft.print("SPD: ");
    tft.print((int)round(mph));
    tft.print(" MPH");

    tft.setCursor(10, 40);
    tft.print("BST: ");
    tft.print(boostPsi, 1);
    tft.print(" ");

    tft.setCursor(10, 70);
    tft.print("IAT: ");
    tft.print((int)round(tempF));
    tft.print(" F");

    tft.setCursor(10, 100);
    tft.print("OIL: ");
    tft.print((int)round(oilPsi));
    tft.print(" PSI");

    tft.setCursor(10, 130);
    tft.print("FUEL: ");
    tft.print(fuelPercent);
    tft.print("%");

    tft.setCursor(10, 160);
    tft.print("TRIP: ");
    tft.print(tripMiles, 1);
    tft.print(" mi");

    if (tripPulseCount >= lastSavedPulseCount + 800) {
      EEPROM.put(EEPROM_ADDR, tripPulseCount);
      lastSavedPulseCount = tripPulseCount;
    }

    lastSpeedMillis = now;
  }

  int reading = digitalRead(BTN_PIN);
  if (reading != lastButtonState) {
    lastDebounceTime = millis();
    lastButtonState = reading;
  }
  if ((millis() - lastDebounceTime) > debounceDelay && lastButtonState == LOW) {
    tripPulseCount = 0;
    EEPROM.put(EEPROM_ADDR, 0);
    lastSavedPulseCount = 0;
  }
}
